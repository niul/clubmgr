apply plugin: 'war'
apply plugin: "com.github.robfletcher.compass"
apply plugin: "js"
apply plugin: "css"

//Project dependencies
dependencies {
	compile project(':clubmgr-data')
	compile 'jstl:jstl:1.2'
	compile 'org.springframework:spring-webmvc:4.2.3.RELEASE'
	compile 'org.springframework:spring-web:4.2.3.RELEASE'
	compile 'org.aspectj:aspectjweaver:1.8.2'
	compile 'javax.validation:validation-api:1.1.0.Final'
	compile 'org.hibernate:hibernate-validator:5.0.1.Final'
	compile 'org.apache.tiles:tiles-api:2.2.2'
	compile 'org.apache.tiles:tiles-core:2.2.2'
	compile 'org.apache.tiles:tiles-jsp:2.2.2'
	compile 'javax.mail:mail:1.4'
	compile 'org.json:json:20140107'
	providedCompile 'javax.servlet:servlet-api:3.0-alpha-1'
	providedCompile 'javax.servlet.jsp:jsp-api:2.1'
}



buildscript {
	repositories {
		jcenter()
		maven { url "http://dl.bintray.com/robfletcher/gradle-plugins" }
	}
	dependencies {
		classpath "com.github.robfletcher:compass-gradle-plugin:2.0.5"
		classpath 'org.gradle.api.plugins:gradle-tomcat-plugin:1.2.3'
		classpath 'com.eriwen:gradle-js-plugin:1.12.1'
		classpath 'com.eriwen:gradle-css-plugin:1.11.1'
		classpath "com.github.jruby-gradle:jruby-gradle-plugin:0.1.+"
	}
}

compass {
	cssDir = file("src/main/webapp/static/css")
	sassDir = file("src/main/webapp/static/css/scss")
}

// Minify CSS
css.source {
	custom {
		css {
			srcDir 'src/main/webapp/static/css'
			include "main.css"
		}
	}
}

minifyCss {
    source = css.source.custom.css.files
    dest = "${projectDir}/src/main/webapp/static/css/main.min.css"
    yuicompressor { // Optional
        lineBreakPos = -1
    }
}


// Minify Javascript
javascript.source {
	custom {
		js {
			srcDir 'src/main/webapp/static/js'
			include "*.js"
			exclude "*.min.js"
		}
	}
}

task individualJSMinify(dependsOn: tasks.matching { Task task -> task.name.startsWith("dominify")})

javascript.source.custom.js.files.eachWithIndex { jsFile, idx ->
	tasks.create(name: "dominify${idx}", type: com.eriwen.gradle.js.tasks.MinifyJsTask) {
		source = jsFile
		dest = "${projectDir}/src/main/webapp/static/js/${jsFile.name}".replaceFirst("\\.js",".min.js")
	}
}

war.dependsOn minifyCss
war.dependsOn individualJSMinify
